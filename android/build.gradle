import org.apache.tools.ant.taskdefs.condition.Os

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        kotlin_version = '1.8.22'

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:7.4.2')
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:5.6.0")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
    
    // Force patched version of assertj-core to fix XXE vulnerability
    // This overrides React Native's vulnerable version (3.21.0)
    configurations.all {
        resolutionStrategy {
            force 'org.assertj:assertj-core:3.27.7'
            eachDependency { details ->
                if (details.requested.group == 'org.assertj' && details.requested.name == 'assertj-core') {
                    details.useVersion '3.27.7'
                    details.because 'CVE fix: XXE vulnerability in versions <= 3.27.6'
                }
            }
        }
    }
    
    // Skip lint tasks for external libraries from node_modules
    // This prevents build failures from third-party library lint issues
    afterEvaluate { project ->
        // Check if project is from node_modules using path separator
        if (project.path.contains(':node_modules:') || project.path.startsWith(':node_modules')) {
            project.tasks.configureEach { task ->
                if (task.name.startsWith('lint')) {
                    task.enabled = false
                }
            }
        }
    }
}
